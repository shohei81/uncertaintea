# Manual Metal GPU smoke test for the device-resident batched logjoint.
#
# NOT part of the package test target and NOT run in CI. Requires a functional Metal
# GPU (Apple Silicon). See test/gpu/Project.toml for setup instructions.
#
# It runs the same parity checks as test/uncertaintea/core/device_lowering_parity.jl, but on a
# Metal.MetalBackend at Float32, and compares against the authoritative CPU
# `batched_logjoint_unconstrained` reference computed in Float64.

using Random
using Test
using UncertainTea
using Metal

@tea static function gpu_gauss_model(n)
    mu ~ normal(0.0, 1.0)
    s ~ gamma(2.0, 1.0)
    for i = 1:n
        {:y => i} ~ normal(mu, s)
    end
    return mu
end

@tea static function gpu_bernoulli_model(n)
    p ~ beta(2.0, 3.0)
    for i = 1:n
        {:z => i} ~ bernoulli(p)
    end
    return p
end

function gpu_check_float32(dev32, ref)
    ok = true
    for (d, r) in zip(dev32, ref)
        ok &= isapprox(Float64(d), r; rtol=1e-3, atol=1e-3 * max(1.0, abs(r)))
    end
    return ok
end

@testset "device Metal GPU smoke" begin
    if !Metal.functional()
        @info "Metal GPU not functional; skipping GPU smoke test."
        @test true
        return
    end

    backend = Metal.MetalBackend()

    # Float64 must be rejected on Metal via the precision guard.
    @test_throws ArgumentError DeviceBatchedWorkspace(gpu_gauss_model, 2; backend=backend, precision=Float64)

    ys = [0.4, -0.7, 1.1, 0.2, 0.9]
    cm = choicemap((:y => i, ys[i]) for i = 1:5)
    params = [0.5 -0.3 1.2 0.05; 0.1 0.7 -0.2 0.4]
    ref = batched_logjoint_unconstrained(gpu_gauss_model, params, (5,), cm)
    dev = device_batched_logjoint(gpu_gauss_model, Float32.(params), (5,), cm; backend=backend, precision=Float32)
    @test gpu_check_float32(dev, ref)

    zs = [1.0, 0.0, 1.0, 1.0]
    cmb = choicemap((:z => i, zs[i]) for i = 1:4)
    pb = reshape([0.3, -0.8, 1.5], 1, 3)
    refb = batched_logjoint_unconstrained(gpu_bernoulli_model, pb, (4,), cmb)
    devb = device_batched_logjoint(gpu_bernoulli_model, Float32.(pb), (4,), cmb; backend=backend, precision=Float32)
    @test gpu_check_float32(devb, refb)
end

@testset "device Metal GPU gradient smoke" begin
    if !Metal.functional()
        @info "Metal GPU not functional; skipping GPU gradient smoke test."
        @test true
        return
    end

    backend = Metal.MetalBackend()

    # gaussian + gamma(log) latent with loop observations.
    ys = [0.4, -0.7, 1.1, 0.2, 0.9]
    cm = choicemap((:y => i, ys[i]) for i = 1:5)
    params = [0.5 -0.3 1.2 0.05; 0.1 0.7 -0.2 0.4]
    gref = batched_logjoint_gradient_unconstrained(gpu_gauss_model, params, (5,), cm)
    vref = batched_logjoint_unconstrained(gpu_gauss_model, params, (5,), cm)
    v, g = device_batched_logjoint_gradient(gpu_gauss_model, Float32.(params), (5,), cm; backend=backend, precision=Float32)
    @test gpu_check_float32(vec(Float64.(g)), vec(gref))
    @test gpu_check_float32(Float64.(v), vref)

    # bernoulli + beta(logit) latent.
    zs = [1.0, 0.0, 1.0, 1.0]
    cmb = choicemap((:z => i, zs[i]) for i = 1:4)
    pb = reshape([0.3, -0.8, 1.5], 1, 3)
    grefb = batched_logjoint_gradient_unconstrained(gpu_bernoulli_model, pb, (4,), cmb)
    _, gb = device_batched_logjoint_gradient(gpu_bernoulli_model, Float32.(pb), (4,), cmb; backend=backend, precision=Float32)
    @test gpu_check_float32(vec(Float64.(gb)), vec(grefb))
end

# --- issue #12 group 1 families: logjoint + gradient parity smoke ------------

@tea static function gpu_heavytail_model(n)
    loc ~ studentt(4.0, 0.0, 1.0)
    s ~ inversegamma(3.0, 2.0)
    w ~ weibull(2.0, 1.5)
    for i = 1:n
        {:y => i} ~ studentt(5.0, loc, s + w)
    end
    return loc
end

@tea static function gpu_count_model(n)
    p ~ beta(2.0, 2.0)
    {:k} ~ binomial(20, p)
    {:g} ~ geometric(p)
    {:nb} ~ negativebinomial(4.0, p)
    for i = 1:n
        {:c => i} ~ categorical(p / 2.0, p / 2.0, 1.0 - p)
    end
    return p
end

@testset "device Metal GPU group-1 family smoke" begin
    if !Metal.functional()
        @info "Metal GPU not functional; skipping GPU group-1 smoke test."
        @test true
        return
    end

    backend = Metal.MetalBackend()

    # continuous: studentt (identity) + inversegamma/weibull (log) latents.
    ys = [0.4, -0.7, 1.1, 0.2, 0.9]
    cm = choicemap((:y => i, ys[i]) for i = 1:5)
    params = [0.5 -0.3 1.2; 0.1 0.7 -0.2; -0.4 0.2 0.6]
    ref = batched_logjoint_unconstrained(gpu_heavytail_model, params, (5,), cm)
    dev = device_batched_logjoint(gpu_heavytail_model, Float32.(params), (5,), cm; backend=backend, precision=Float32)
    @test gpu_check_float32(dev, ref)
    gref = batched_logjoint_gradient_unconstrained(gpu_heavytail_model, params, (5,), cm)
    _, g = device_batched_logjoint_gradient(
        gpu_heavytail_model,
        Float32.(params),
        (5,),
        cm;
        backend=backend,
        precision=Float32,
    )
    @test gpu_check_float32(vec(Float64.(g)), vec(gref))

    # discrete: binomial / geometric / negativebinomial / categorical observations.
    cs = [1.0, 3.0, 2.0, 3.0]
    cm2 = choicemap((:k, 7.0), (:g, 3.0), (:nb, 5.0), ((:c => i, cs[i]) for i = 1:4)...)
    params2 = reshape([0.3, -0.8, 1.5], 1, 3)
    ref2 = batched_logjoint_unconstrained(gpu_count_model, params2, (4,), cm2)
    dev2 = device_batched_logjoint(gpu_count_model, Float32.(params2), (4,), cm2; backend=backend, precision=Float32)
    @test gpu_check_float32(dev2, ref2)
    g2ref = batched_logjoint_gradient_unconstrained(gpu_count_model, params2, (4,), cm2)
    _, g2 = device_batched_logjoint_gradient(
        gpu_count_model,
        Float32.(params2),
        (4,),
        cm2;
        backend=backend,
        precision=Float32,
    )
    @test gpu_check_float32(vec(Float64.(g2)), vec(g2ref))
end

# --- issue #12 group 2: truncated families smoke ------------------------------

@tea static function gpu_truncated_model(n)
    m ~ normal(0.0, 1.0)
    s ~ gamma(2.0, 2.0)
    {:h} ~ truncatednormal(m, 1.0, 0.0, Inf)
    for i = 1:n
        {:y => i} ~ truncatednormal(m, s, -1.0, 2.0)
        {:t => i} ~ truncatedstudentt(5.0, m, s, -2.0, 2.0)
    end
    return m
end

@testset "device Metal GPU truncated family smoke" begin
    if !Metal.functional()
        @info "Metal GPU not functional; skipping GPU truncated smoke test."
        @test true
        return
    end

    backend = Metal.MetalBackend()
    ys = [0.4, -0.7, 1.1]
    ts = [1.5, -0.2, 0.8]
    cm = choicemap((:h, 0.6), ((:y => i, ys[i]) for i = 1:3)..., ((:t => i, ts[i]) for i = 1:3)...)
    params = [0.5 -0.3 1.2; 0.1 0.7 -0.2]
    ref = batched_logjoint_unconstrained(gpu_truncated_model, params, (3,), cm)
    dev = device_batched_logjoint(gpu_truncated_model, Float32.(params), (3,), cm; backend=backend, precision=Float32)
    @test gpu_check_float32(dev, ref)
    gref = batched_logjoint_gradient_unconstrained(gpu_truncated_model, params, (3,), cm)
    _, g = device_batched_logjoint_gradient(
        gpu_truncated_model,
        Float32.(params),
        (3,),
        cm;
        backend=backend,
        precision=Float32,
    )
    @test gpu_check_float32(vec(Float64.(g)), vec(gref))
end

# --- device-resident batched HMC / ADVI smoke (PR 46) ------------------------
# Mirrors test/uncertaintea/core/device_hmc_advi.jl on a Metal.MetalBackend at Float32.
# RNG stays host-side, so results are statistically (not bitwise) equivalent to the
# CPU path; we only assert finite results and posterior/variational-mean sanity.

@tea static function gpu_conjugate_gauss()
    mu ~ normal(0.0, 1.0)
    {:y} ~ normal(mu, 1.0)
    return mu
end

@testset "device Metal GPU batched HMC/ADVI smoke" begin
    if !Metal.functional()
        @info "Metal GPU not functional; skipping GPU HMC/ADVI smoke test."
        @test true
        return
    end

    backend = Metal.MetalBackend()
    constraints = choicemap((:y, 0.3))

    chains = batched_hmc(
        gpu_conjugate_gauss,
        (),
        constraints;
        num_chains=2,
        num_samples=300,
        num_warmup=150,
        backend=backend,
        precision=Float32,
        rng=MersenneTwister(46),
    )
    samples = posterior_array(chains)
    @test all(isfinite, samples)
    @test isapprox(sum(samples) / length(samples), 0.15; atol=0.15)
    @test all(<(1.3), rhat(chains))

    result = batched_advi(
        gpu_conjugate_gauss,
        (),
        constraints;
        num_steps=300,
        backend=backend,
        precision=Float32,
        rng=MersenneTwister(46),
    )
    @test all(isfinite, result.elbo_history)
    @test isapprox(variational_mean(result)[1], 0.15; atol=0.15)
end

# --- device-resident masked batched NUTS smoke (issue #2) --------------------
# Mirrors test/uncertaintea/core/device_masked_nuts.jl on a Metal.MetalBackend at Float32.
# The masked doubling trajectory runs device-resident: the per-leaf P x C arrays
# and tree ops (leapfrog, checkpoints, U-turn) live on the device, while the RNG
# draws and O(num_chains) bookkeeping stay host-side, so results are statistically
# (not bitwise) equivalent to the CPU masked path.

@testset "device Metal GPU batched NUTS smoke" begin
    if !Metal.functional()
        @info "Metal GPU not functional; skipping GPU NUTS smoke test."
        @test true
        return
    end

    backend = Metal.MetalBackend()
    constraints = choicemap((:y, 0.3))

    chains = batched_nuts(
        gpu_conjugate_gauss,
        (),
        constraints;
        num_chains=2,
        num_samples=300,
        num_warmup=150,
        tree_strategy=:masked,
        backend=backend,
        precision=Float32,
        rng=MersenneTwister(46),
    )
    samples = posterior_array(chains)
    @test all(isfinite, samples)
    @test isapprox(sum(samples) / length(samples), 0.15; atol=0.15)
    @test all(<(1.3), rhat(chains))
end

@tea static function gpu_noncentered_funnel()
    v ~ normal(0.0, 3.0)
    x ~ normal(0.0, exp(v / 2); reparam=:noncentered)
    {:y} ~ normal(x, 10.0)
    return v
end

@testset "device Metal GPU noncentered normal parity" begin
    if !Metal.functional()
        @info "Metal GPU not functional; skipping GPU smoke test."
        @test true
        return
    end
    constraints = choicemap((:y, 0.5))
    points = Float64[0.3 -0.5; 0.9 0.2]
    supported, _ = device_lowering_report(gpu_noncentered_funnel)
    @test supported
    values, gradients = device_batched_logjoint_gradient(
        gpu_noncentered_funnel,
        Float32.(points),
        (),
        constraints;
        backend=Metal.MetalBackend(),
        precision=Float32,
    )
    reference_values =
        [logjoint_unconstrained(gpu_noncentered_funnel, points[:, i], (), constraints) for i = 1:2]
    reference_gradients = hcat(
        [logjoint_gradient_unconstrained(gpu_noncentered_funnel, points[:, i], (), constraints) for i = 1:2]...,
    )
    # Metal runs Float32
    @test Float64.(collect(values)) ≈ reference_values atol = 1e-4
    @test Float64.(collect(gradients)) ≈ reference_gradients atol = 1e-4
end
